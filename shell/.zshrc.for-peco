export FUZZY_FINDER=peco

# Disable flow control to use Ctrl-S/Ctrl-Q for other purposes (e.g. fzf)
if [[ -t 1 ]]; then
  stty -ixon -ixoff
fi
unsetopt FLOWCONTROL

## select-history
function peco-select-history() {
  local tac
  if which tac > /dev/null; then
    tac="tac"
  else
    tac="tail -r"
  fi
  BUFFER=$(\history -n 1 | \
    eval $tac | \
    $FUZZY_FINDER --query "$LBUFFER")
  CURSOR=$#BUFFER
  zle clear-screen
}
zle -N peco-select-history
bindkey '^r' peco-select-history

## pushd
function peco-pushd() {
  eval cd $(pushd | sed -e "s/ /\n/g" | $FUZZY_FINDER)
  zle reset-prompt
}
zle -N peco-pushd
bindkey "^a" peco-pushd

## src
function peco-src () {
  local selected_dir=$(ghq list --full-path | $FUZZY_FINDER --query "$LBUFFER")
  if [ -n "$selected_dir" ]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-src
bindkey '^s' peco-src

## function-list
function peco-function-list () {
  local selected=$(functions | grep "^.*\ ()\ {" | sed -e "s| () {||" | grep peco- | grep -v function-list | $FUZZY_FINDER --query "$LBUFFER")
  if [ -n "$selected" ]; then
    ${selected}
  fi
}
zle -N peco-function-list
bindkey '^p' peco-function-list


## cd
function peco-cd() {
  local var
  local dir
  if [ ! -t 0 ]; then
    var=$(cat -)
    dir=$(echo -n $var | $FUZZY_FINDER)
  else
    return 1
  fi

  if [ -d "$dir" ]; then
    cd "$dir"
  else
    echo "'$dir' was not directory." >&2
    return 1
  fi
}

## open
function peco-open() {
  local var
  local file
  local command="open"
  if [ ! -t 0 ]; then
    var=$(cat -)
    file=$(echo -n $var | $FUZZY_FINDER)
  else
    return 1
  fi

  if [ -n "$1" ]; then
    command="$1"
  fi

  if [ -e "$file" ]; then
    eval "$command $file"
  else
    echo "Could not open '$file'." >&2
    return 1
  fi
}

## git-diff
function peco-git-diff() {
  git rev-parse --git-dir >/dev/null 2>&1
  if [[ $? == 0 ]]; then
    local target
    if [[ $# -eq 0 ]]; then
      target=$(git diff --stat $(git symbolic-ref HEAD 2>/dev/null) | $FUZZY_FINDER | awk '{print $1}')
    else
      target=$(git diff --stat $1 | $FUZZY_FINDER | awk '{print $1}')
    fi
    if [[ -n $target ]]; then
      vimdiff <(git show $1:$target) $target
    fi
  fi
}

